      SUBROUTINE DEB_BABY

C     NICHEMAPR: SOFTWARE FOR BIOPHYSICAL MECHANISTIC NICHE MODELLING

C     COPYRIGHT (C) 2018 MICHAEL R. KEARNEY AND WARREN P. PORTER

C     THIS PROGRAM IS FREE SOFTWARE: YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C     IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C     THE FREE SOFTWARE FOUNDATION, EITHER VERSION 3 OF THE LICENSE, OR (AT
C      YOUR OPTION) ANY LATER VERSION.

C     THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C     WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C     MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C     GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C     YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C     ALONG WITH THIS PROGRAM. IF NOT, SEE HTTP://WWW.GNU.ORG/LICENSES/.

C     IMPLEMENTATION OF KOOIJMAN'S KAPPA-RULE STANDARD DEB MODEL FOR VIVIPAROUSLY DEVELOPING OFFSPRING

      IMPLICIT NONE

      DOUBLE PRECISION ANDENS_DEB,BREEDRAINTHRESH,CLUTCHA,CLUTCHB
      DOUBLE PRECISION CLUTCHSIZE,CTMAX,CTMIN,CUMBATCH_INIT
      DOUBLE PRECISION CUMREPRO_INIT,D_V,DAYLENGTHFINISH,DAYLENGTHSTART
      DOUBLE PRECISION DE_HDT,DEDT,DELTA_DEB,DLDT,DUEDT,DUHDT,DVDT
      DOUBLE PRECISION E_BABY,E_BABY_INIT,E_EGG,E_G,E_H_INIT,E_HE
      DOUBLE PRECISION E_H_PRES,E_H_START,E_HB,E_HJ,E_HP,E_HPUP_INIT
      DOUBLE PRECISION E_INIT,E_INIT_BABY,E_M,E_PRES,E_SCALED,E_TEMP
      DOUBLE PRECISION EGGDRYFRAC,EH_BABY,EH_BABY_INIT,EPUP_INIT,ES_INIT
      DOUBLE PRECISION ESM,F,FUNCT,G,H_AREF,HALFSAT,HS_INIT,K_J,K_JREF
      DOUBLE PRECISION K_M,KAP_R,KAP_X,KAP_X_P,KAPPA,L_B,L_J,L_M,L_PRES
      DOUBLE PRECISION L_T,LAMBDA,LAT,LENGTHDAY,LENGTHDAYDIR,M_V,MAXMASS
      DOUBLE PRECISION MU_E,MU_P,MU_V,MU_X,P_AM,P_MREF,P_MV,P_XMREF
      DOUBLE PRECISION PREVDAYLENGTH,Q_INIT,S_G,S_J,SC,SCALED_L
      DOUBLE PRECISION SURVIV_INIT,T_A,T_AH,T_AL,T_H,T_L,T_REF,TB,TC
      DOUBLE PRECISION TCORR,U_H_PRES,V_BABY,V_BABY_INIT,V_INIT
      DOUBLE PRECISION V_INIT_BABY,V_M,V_PRES,V_TEMP,VDOT,VDOTREF
      DOUBLE PRECISION VOLD_INIT,VPUP_INIT,W_E,W_N,W_P,W_V,W_X,X_FOOD
      DOUBLE PRECISION Y_EV_L,ZFACT

      INTEGER AQUABREED,AQUASTAGE,BATCH,BREEDACT,BREEDACTTHRES,BREEDING
      INTEGER BREEDVECT,COUNTDAY,CTKILL,CTMINCUM,CTMINTHRESH,DAYCOUNT
      INTEGER METAB_MODE,METAMORPH,PHOTODIRF,PHOTODIRS,PHOTOFINISH
      INTEGER PHOTOSTART,PREGNANT,STAGES,VIVIPAROUS
      
      DIMENSION BREEDVECT(24)
      
      COMMON/ARRHEN/T_A,T_AL,T_AH,T_L,T_H,T_REF
      COMMON/BREEDER/BREEDING,BREEDVECT
      COMMON/COUNTDAY/COUNTDAY,DAYCOUNT
      COMMON/CTMAXMIN/CTMAX,CTMIN,CTMINCUM,CTMINTHRESH,CTKILL
      COMMON/DEBBABY/V_BABY,E_BABY,EH_BABY
      COMMON/DEBINIT1/V_INIT,E_INIT,CUMREPRO_INIT,CUMBATCH_INIT,
     &  VOLD_INIT,VPUP_INIT,EPUP_INIT
      COMMON/DEBINIT2/ES_INIT,Q_INIT,HS_INIT,P_MREF,VDOTREF,H_AREF,
     & E_BABY_INIT,V_BABY_INIT,EH_BABY_INIT,K_JREF,S_G,SURVIV_INIT,
     & HALFSAT,X_FOOD,E_HPUP_INIT,P_XMREF
      COMMON/DEBPAR1/CLUTCHSIZE,ANDENS_DEB,D_V,EGGDRYFRAC,W_E,MU_E,MU_V,
     & W_V,E_EGG,KAP_X,KAP_X_P,MU_X,MU_P,W_N,W_P,W_X,FUNCT
      COMMON/DEBPAR2/ZFACT,KAPPA,E_G,KAP_R,DELTA_DEB,E_H_START,MAXMASS,
     & E_INIT_BABY,V_INIT_BABY,E_H_INIT,E_HB,E_HP,E_HJ,ESM,LAMBDA,
     & BREEDRAINTHRESH,DAYLENGTHSTART,DAYLENGTHFINISH,LENGTHDAY,
     & LENGTHDAYDIR,PREVDAYLENGTH,LAT,CLUTCHA,CLUTCHB,E_HE,
     & AQUABREED,AQUASTAGE,PHOTODIRS,PHOTODIRF,
     & BREEDACTTHRES,METAMORPH,PHOTOSTART,PHOTOFINISH,BREEDACT,BATCH
      COMMON/DEBPAR3/METAB_MODE,STAGES,Y_EV_L
      COMMON/DEBPAR4/S_J,L_B,L_J
      COMMON/TREG/TC
      COMMON/VIVIP/VIVIPAROUS,PREGNANT

C     BODY TEMPERATURE
      TB = MIN(CTMAX, TC) ! DON'T LET IT GO TOO HIGH
      
C     ARRHENIUS TEMPERATURE CORRECTION FACTOR
C       TCORR = EXP(T_A*(1/(273+T_REF)-1/(273+TB)))/(1+EXP(T_AL
C     & *(1/(273+TB)-1/T_L))+EXP(T_AH*(1/T_H-1/(273+TB))))
       TCORR = EXP(T_A/(273.15+T_REF)-T_A/(273.15+TB))*((1+EXP(T_AL/
     &(273.15+T_REF)-T_AL/T_L)+EXP(T_AH/T_H-T_AH/(273.15+T_REF)))/(1+EXP
     &(T_AL/(273.15+TB)-T_AL/T_L)+EXP(T_AH/T_H-T_AH/(273.15+TB))))
     
      V_PRES = V_BABY
      E_PRES = E_BABY
      E_H_PRES = EH_BABY

      IF(V_PRES.EQ.V_INIT_BABY)THEN
       E_PRES=E_EGG/V_PRES
      ENDIF
      F = 1.

C     TEMPERATURE CORRECTIONS AND COMPOUND PARAMETERS
      M_V = ANDENS_DEB/W_V
      P_MV = P_MREF*TCORR
      K_M = P_MV/E_G
      K_J = K_JREF*TCORR
      P_AM = P_MV*ZFACT/KAPPA
      VDOT = VDOTREF*TCORR
      E_M = P_AM/VDOT
      G = E_G/(KAPPA*E_M)
      E_SCALED=E_PRES/E_M
      V_M = (KAPPA*P_AM/P_MV)**(3.)
      L_T = 0.
      L_PRES = V_PRES**(1./3.)
      L_M = V_M**(1./3.)
      SCALED_L = L_PRES/L_M

C     EMBRYO EQUATION FOR LENGTH, FROM KOOIJMAN 2009 EQ. 2
      DLDT = (VDOT*E_SCALED-K_M*G*V_PRES**(1./3.))/(3*(E_SCALED+G))
      V_TEMP=(V_PRES**(1./3.)+DLDT)**3
      DVDT = V_TEMP-V_PRES

C     EMBRYO EQUATION FOR SCALED RESERVE, U_E, FROM KOOIJMAN 2009 EQ. 1
      SC=L_PRES**2.*(G*E_SCALED)/(G+E_SCALED)*(1+((K_M*L_PRES)/VDOT))
      DUEDT=-1*SC
      E_TEMP=((E_PRES*V_PRES/P_AM)+DUEDT)*P_AM/(V_PRES+DVDT)
      DEDT=E_TEMP-E_PRES

C     EMBRYO EQUATION FOR SCALED MATURITY, U_H, FROM KOOIJMAN 2009 EQ. 3
      U_H_PRES=E_H_PRES/P_AM
      DUHDT=(1-KAPPA)*SC-K_J*U_H_PRES
      DE_HDT=DUHDT*P_AM

      V_BABY = V_PRES+DVDT
      E_BABY = E_PRES+DEDT
      EH_BABY = E_H_PRES+DE_HDT
C     MAKE SURE ED DOESN'T GO BELOW ZERO
      IF(E_BABY.LT.0)THEN
       E_BABY=0
      ENDIF

      RETURN
      END



